#pragma kernel CSMain

RWTexture2D<float4> Result;

float4x4 _CameraToWorld;
float4x4 _CameraInverseProjection;

float4 _DirectionalLight;

float2 _PixelOffset;

Texture2D<float4> _SkyboxTexture;
SamplerState sampler_SkyboxTexture;

static const float PI = 3.14159265f;
static const float EPSILON = 1e-8;

//-------------------------------------
//- UTILITY

float sdot(float3 _x, float3 _y, float _f = 1.0f)
{
    return saturate(dot(_x, _y) * _f);
}

float energy(float3 _colour)
{
    return dot(_colour, 1.0f / 3.0f);
}

//-------------------------------------
//- RANDOMNESS

float2 _Pixel;
float _Seed;

float rand()
{
    float result = frac(sin(_Seed / 100.0f * dot(_Pixel, float2(12.9898f, 78.233f))) * 43758.5453f);
    _Seed += 1.0f;
    return result;
}


//-------------------------------------
//- SPHERES

struct Sphere
{
    float3 position;
    float radius;
    float3 albedo;
    float3 specular;
    float smoothness;
    float3 emission;
};

StructuredBuffer<Sphere> _Spheres;


//-------------------------------------
//- MESHES

struct MeshObject
{
    float4x4 localToWorldMatrix;
    int indices_offset;
    int indices_count;
    float3 albedo;
    float3 specular;
    float smoothness;
};

StructuredBuffer<MeshObject> _MeshObjects;
StructuredBuffer<float3> _Vertices;
StructuredBuffer<int> _Indices;


//-------------------------------------
//- RAY

struct Ray
{
    float3 origin;
    float3 direction;
    float3 energy;
};

Ray CreateRay(float3 _origin, float3 _direction)
{
    Ray ray;
    ray.origin = _origin;
    ray.direction = _direction;
    ray.energy = float3(1.0f, 1.0f, 1.0f);
    return ray;
}

Ray CreateCameraRay(float2 _uv)
{
    // Transform the camera origin to world space
    float3 origin = mul(_CameraToWorld, float4(0.0f, 0.0f, 0.0f, 1.0f)).xyz;
    
    // Invert the perspective projection of the view-space position
    float3 direction = mul(_CameraInverseProjection, float4(_uv, 0.0f, 1.0f)).xyz;
    // Transform the direction from camera to world space and normalize
    direction = mul(_CameraToWorld, float4(direction, 0.0f)).xyz;
    direction = normalize(direction);

    return CreateRay(origin, direction);
}


//-------------------------------------
//- RAYHIT

struct RayHit
{
    float3 position;
    float distance;
    float3 normal;
    float3 albedo;
    float3 specular;
    float smoothness;
    float3 emission;
};

RayHit CreateRayHit()
{
    RayHit hit;
    hit.position = float3(0.0f, 0.0f, 0.0f);
    hit.distance = 1.#INF;
    hit.normal = float3(0.0f, 0.0f, 0.0f);
    hit.albedo = float3(0.0f, 0.0f, 0.0f);
    hit.specular = float3(0.0f, 0.0f, 0.0f);
    hit.smoothness = 0.0f;
    hit.emission = float3(0.0f, 0.0f, 0.0f);
    return hit;
}


//-------------------------------------
//- INTERSECTION

void IntersectGroundPlane(Ray _ray, inout RayHit _bestHit)
{
    // Calculate distance along the ray where the ground plane is intersected
    float t = -_ray.origin.y / _ray.direction.y;
    if (t > 0 && t < _bestHit.distance)
    {
        _bestHit.distance = t;
        _bestHit.position = _ray.origin + t * _ray.direction;
        _bestHit.normal = float3(0.0f, 1.0f, 0.0f);
        _bestHit.albedo = 0.5f;
        _bestHit.specular = 0.03f;
        _bestHit.smoothness = 0.2f;
        _bestHit.emission = float3(0.0f, 0.0f, 0.0f);
    }
}

void IntersectSphere(Ray _ray, inout RayHit _bestHit, Sphere _sphere)
{
    // Calculate distance along the ray where the sphere is intersected
    float3 d = _ray.origin - _sphere.position;
    float p1 = -dot(_ray.direction, d);
    float p2sqr = p1 * p1 - dot(d, d) + _sphere.radius * _sphere.radius;
    if (p2sqr < 0)
        return;
    float p2 = sqrt(p2sqr);
    float t = p1 - p2 > 0 ? p1 - p2 : p1 + p2;
    if (t > 0 && t < _bestHit.distance)
    {
        _bestHit.distance = t;
        _bestHit.position = _ray.origin + t * _ray.direction;
        _bestHit.normal = normalize(_bestHit.position - _sphere.position);
        _bestHit.albedo = _sphere.albedo;
        _bestHit.specular = _sphere.specular;
        _bestHit.smoothness = _sphere.smoothness;
        _bestHit.emission = _sphere.emission;
    }
}

bool IntersectTriangle(Ray _ray, float3 _vert0, float3 _vert1, float3 _vert2, inout float _t, inout float _u, inout float _v)
{
	// find vectors for two edges sharing vert0
    float3 edge1 = _vert1 - _vert0;
    float3 edge2 = _vert2 - _vert0;

	// begin calculating determinant - also used to calculate U parameter
    float3 pvec = cross(_ray.direction, edge2);

	// if determinant is near zero, ray lies in plane of triangle
    float det = dot(edge1, pvec);

	// use backface culling
    if (det < EPSILON)
        return false;
    float inv_det = 1.0f / det;

	// calculate distance from vert0 to ray origin
    float3 tvec = _ray.origin - _vert0;

	// calculate U parameter and test bounds
    _u = dot(tvec, pvec) * inv_det;
    if (_u < 0.0 || _u > 1.0f)
        return false;

	// prepare to test V parameter
    float3 qvec = cross(tvec, edge1);

	// calculate V parameter and test bounds
    _v = dot(_ray.direction, qvec) * inv_det;
    if (_v < 0.0 || _u + _v > 1.0f)
        return false;

	// calculate t, ray intersects triangle
    _t = dot(edge2, qvec) * inv_det;

    return true;
}

void IntersectMeshObject(Ray _ray, inout RayHit _bestHit, MeshObject _meshObject)
{
    uint offset = _meshObject.indices_offset;
    uint count = offset + _meshObject.indices_count;
    for (uint i = offset; i < count; i += 3)
    {
        float3 v0 = (mul(_meshObject.localToWorldMatrix, float4(_Vertices[_Indices[i]], 1))).xyz;
        float3 v1 = (mul(_meshObject.localToWorldMatrix, float4(_Vertices[_Indices[i + 1]], 1))).xyz;
        float3 v2 = (mul(_meshObject.localToWorldMatrix, float4(_Vertices[_Indices[i + 2]], 1))).xyz;

        float t, u, v;
        if (IntersectTriangle(_ray, v0, v1, v2, t, u, v))
        {
            if (t > 0 && t < _bestHit.distance)
            {
                _bestHit.distance = t;
                _bestHit.position = _ray.origin + t * _ray.direction;
                _bestHit.normal = normalize(cross(v1 - v0, v2 - v0));
                _bestHit.albedo = _meshObject.albedo;
                _bestHit.specular = _meshObject.specular;
                _bestHit.smoothness = 0.99f;
            }
        }
    }
}

//-------------------------------------
//- TRACE

RayHit Trace(Ray _ray)
{
    RayHit bestHit = CreateRayHit();
    uint count, stride, i;

	// Trace ground plane
    IntersectGroundPlane(_ray, bestHit);

	// Trace spheres
    _Spheres.GetDimensions(count, stride);
    for (i = 0; i < count; i++)
    {
        IntersectSphere(_ray, bestHit, _Spheres[i]);
    }

	// Trace mesh objects
    _MeshObjects.GetDimensions(count, stride);
    for (i = 0; i < count; i++)
    {
        IntersectMeshObject(_ray, bestHit, _MeshObjects[i]);
    }

    return bestHit;
}


//-------------------------------------
//- SAMPLING

float3x3 GetTangentSpace(float3 _normal)
{
    // Choose a helper vector for the cross product
    float3 helper = float3(1, 0, 0);
    if (abs(_normal.x) > 0.99f)
        helper = float3(0, 0, 1);

    // Generate vectors
    float3 tangent = normalize(cross(_normal, helper));
    float3 binormal = normalize(cross(_normal, tangent));
    return float3x3(tangent, binormal, _normal);
}

float3 SampleHemisphere(float3 _normal, float _alpha)
{
    // Sample the hemisphere, where alpha determines the kind of the sampling
    float cosTheta = pow(rand(), 1.0f / (_alpha + 1.0f));
    float sinTheta = sqrt(1.0f - cosTheta * cosTheta);
    float phi = 2 * PI * rand();
    float3 tangentSpaceDir = float3(cos(phi) * sinTheta, sin(phi) * sinTheta, cosTheta);

    // Transform direction to world space
    return mul(tangentSpaceDir, GetTangentSpace(_normal));
}

//-------------------------------------
//- SHADE

float SmoothnessToPhongAlpha(float _s)
{
    return pow(1000.0f, _s * _s);
}

float3 Shade(inout Ray _ray, RayHit _hit)
{
    if (_hit.distance < 1.#INF)
    {
        // Calculate chances of diffuse and specular reflection
        _hit.albedo = min(1.0f - _hit.specular, _hit.albedo);
        float specChance = energy(_hit.specular);
        float diffChance = energy(_hit.albedo);

        // Roulette-select the ray's path
        float roulette = rand();
        if (roulette < specChance)
        {
            // Specular reflection
            _ray.origin = _hit.position + _hit.normal * 0.001f;
            float alpha = SmoothnessToPhongAlpha(_hit.smoothness);
            _ray.direction = SampleHemisphere(reflect(_ray.direction, _hit.normal), alpha);
            float f = (alpha + 2) / (alpha + 1);
            _ray.energy *= (1.0f / specChance) * _hit.specular * sdot(_hit.normal, _ray.direction, f);
        }
        else if (diffChance > 0 && roulette < specChance + diffChance)
        {
            // Diffuse reflection
            _ray.origin = _hit.position + _hit.normal * 0.001f;
            _ray.direction = SampleHemisphere(_hit.normal, 1.0f);
            _ray.energy *= (1.0f / diffChance) * _hit.albedo;
        }
        else
        {
            // Terminate ray
            _ray.energy = 0.0f;
        }

        return _hit.emission;
    }
    else
    {
        // Erase the ray's energy - the sky doesn't reflect anything
        _ray.energy = 0.0f;

        // Sample the skybox and write it
        float theta = acos(_ray.direction.y) / -PI;
        float phi = atan2(_ray.direction.x, -_ray.direction.z) / -PI * 0.5f;
        return _SkyboxTexture.SampleLevel(sampler_SkyboxTexture, float2(phi, theta), 0).xyz;
    }
}


//-------------------------------------
//- KERNEL

[numthreads(8, 8, 1)]
void CSMain(uint3 id : SV_DispatchThreadID)
{
    _Pixel = id.xy;

    // Get the dimensions of the RenderTexture
    uint width, height;
    Result.GetDimensions(width, height);

    // Transform pixel to [-1,1] range
    float2 uv = float2((id.xy + _PixelOffset) / float2(width, height) * 2.0f - 1.0f);

    // Get a ray for the UVs
    Ray ray = CreateCameraRay(uv);

    // Trace and shade the ray
    float3 result = float3(0, 0, 0);
    for (int i = 0; i < 8; i++)
    {
        RayHit hit = Trace(ray);
        result += ray.energy * Shade(ray, hit);

        if (!any(ray.energy))
            break;
    }

    Result[id.xy] = float4(result, 1);
}
